pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        maven{ url = 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven{ url = "https://dl.bintray.com/kotlin/kotlin-dev" }
    }
    resolutionStrategy {
        eachPlugin {
            switch (requested.id.id) {
                case "com.android.library":
                case "com.android.application" :
                    def ver = requested.version!=null ? requested.version : androidBuildToolsVersion
                    useModule("com.android.tools.build:gradle:${ver}");
                    break
                case "org.jetbrains.kotlin.android":
                case "kotlin-android-extensions" :
                    def ver = requested.version!=null ? requested.version : kotlinVersion
                    useModule("org.jetbrains.kotlin:kotlin-gradle-plugin:${ver}");
                    break
                case "org.jetbrains.dokka-android" :
                    def ver = requested.version!=null ? requested.version : dokkaVersion
                    useModule("org.jetbrains.dokka:dokka-android-gradle-plugin:${ver}")
                    break
                case "com.jfrog.bintray":
                    useVersion(requested.version!=null ? requested.version : bintrayVersion)
            }
        }
    }
}

include(":testapp", ":core", ":appcompat")
//rootProject.name="android-coroutines"